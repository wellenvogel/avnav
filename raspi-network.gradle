
import java.text.SimpleDateFormat;
import org.redline_rpm.header.*;
def taskgroup="avnav"

buildscript{
	repositories{
		mavenCentral()
	}
    dependencies{
        classpath 'com.netflix.nebula:gradle-ospackage-plugin:12.1.0'
    }
}

repositories{
	mavenCentral()
}

description="Raspberry specfic stuff for avnav (new)"
apply plugin: 'java'
apply plugin: 'com.netflix.nebula.ospackage-base'

def versionFileName="raspi.version"
def BASE='raspberry'
def NWSUB='network-nm'
def BASENW="$BASE/$NWSUB"
def NWM="/etc/NetworkManager"
def NWMCON="$NWM/system-connections"
def NWMCFG="$NWM/conf.d"
def NWMAVNAV="99-avnav.conf"
def POLKIT="/etc/polkit-1/rules.d"
def AVNPOLKIT="00-nmall.rules"
def CONNECTIONS=["Ethernet.nmconnection","Hotspot.nmconnection"]
def TARGET='/usr/lib/avnav/raspberry'
task build_raspi_network(type:Deb) {
    assert project.hasProperty('avnavVersion'),"please provide the package version via -PavnavVersion=xxx"
    version=project.avnavVersion
    outputs.upToDateWhen { t->
        def f=t.determineArchivePath();
        if (! f.exists()) {
            println("outfile $f not found, must execute")
            return false;
        }
        return true;
    }
    group taskgroup
    doFirst{
        CONNECTIONS.each{ String c->
            configurationFile("$NWMCON/$c")
        }
        configurationFile("$NWMCFG/$NWMAVNAV")
        configurationFile("$POLKIT/$AVNPOLKIT")
    }
    packageGroup='misc'
    requires 'avnav-raspi-base'
    requires 'python3-smbus' 
    requires 'jq'
    requires('network-manager','1.52',Flags.GREATER|Flags.EQUAL)
    requires 'systemd-resolved'
    requires 'firewalld'
    requires 'avahi-autoipd'
    packageName='avnav-raspi-network'
    configurationFile("/etc/udev/rules.d/010-avnav-net.rules")
    user='root'
    into ("$TARGET/$NWSUB") {
        from(BASENW){
            fileMode 0755
            include 'set-network.py' 
        }
        from(BASENW) {
            include CONNECTIONS
            fileMode 0644
        }
    }
    into(NWMCFG){
        from(BASENW) {
            include NWMAVNAV
            fileMode 0644
        }
    }
    into(POLKIT){
        from(BASENW){
            fileMode 0644
            include AVNPOLKIT
        }
    }
    into('/etc/avahi/services'){
        from('raspberry'){
            include('avnav-ssh.service')
        }
    }
    into('/etc/sysctl.d'){
        from('raspberry'){
            include('99-zzavnavsysctl.conf')
        }
    }
    postInstall file("$BASENW/postinstall")
    
}



