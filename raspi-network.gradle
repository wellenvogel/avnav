
import java.text.SimpleDateFormat;
import org.redline_rpm.header.*;
def taskgroup="avnav"

buildscript{
	repositories{
		mavenCentral()
	}
    dependencies{
        classpath 'com.netflix.nebula:gradle-ospackage-plugin:12.1.0'
    }
}

repositories{
	mavenCentral()
}

description="Raspberry specfic stuff for avnav (new)"
apply plugin: 'java'
apply plugin: 'com.netflix.nebula.ospackage-base'

def versionFileName="raspi.version"
def BASE='raspberry'
def BASENW="$BASE/network-nm"
def NWM="/etc/NetworkManager"
def NWMCON="$NWM/system-connections"
def NWMCFG="$NWM/conf.d"
def NWMAVNAV="99-avnav.conf"
def CONNECTIONS=["Bridge.nmconnection","Ethernet.nmconnection","Hotspot.nmconnection"]
task build_raspi_network(type:Deb) {
    group taskgroup
    doFirst{
        assert project.hasProperty('avnavVersion'),"please provide the package version via -PavnavVersion=xxx"
        version=project.avnavVersion
        CONNECTIONS.each{ String c->
            configurationFile("$NWMCON/$c")
        }
        configurationFile("$NWMCFG/$NWMAVNAV")
    }
    //release='3'
    os = LINUX // only applied to RPM
    packageGroup='misc'
    //requires 'avnav'
    //requires 'avnav-raspi-base'
    requires 'python3-smbus' 
    requires 'jq'
    requires('network-manager','1.52',Flags.GREATER|Flags.EQUAL)
    requires 'systemd-resolved'
    requires 'firewalld'
    requires 'avahi-autoipd'
    packageName='avnav-raspi-network'
    configurationFile("/etc/udev/rules.d/010-avnav-net.rules")
    user='root'
    into ('/usr/lib/avnav/raspberry') {
        from(BASE){
            fileMode 0755
            include 'notfound' //dummy
        }
        
    }
    into(NWMCON){
        from("$BASENW") {
            include CONNECTIONS
            fileMode 0644
        }
    }
    into(NWMCFG){
        from("$BASENW") {
            include NWMAVNAV
            fileMode 0644
        }
    }
    into('/etc/avahi/services'){
        from('raspberry'){
            include('avnav-ssh.service')
        }
    }
    postInstall file("$BASENW/postinstall")
    
}



