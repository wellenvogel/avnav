import java.text.SimpleDateFormat
import com.moowork.gradle.node.task.*
buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.moowork.gradle:gradle-node-plugin:0.12"
    }
}

apply plugin: 'com.moowork.node'
apply plugin: 'java'

class MyNpm extends NpmTask{
    def subDir
    @InputFiles
    def getInputFiles(){
        logger.debug "getInputFiles called for $name"
        return project.fileTree(project.projectDir,{
            exclude "build/**"
            exclude ".gradle/**"
        })
    }
    @OutputFiles
    def getOutputFiles(){
        logger.debug "getOutputFiles called for $name"
        return project.fileTree(project.projectDir,{
            include "build/$subDir/**"
        })
    }
    @OutputDirectory
    def getOutDir(){
        return project.file("build/$subDir")
    }
    public MyNpm(){
        outputs.upToDateWhen{
            def od=getOutDir()
            logger.debug "checking $od"
            if (! od.exists()) return false;
            return true
        }
    }
}
project.ext.avnavVersion=null
File versionFile=new File(project.buildDir,"version.js")
task pkgVersion {
    doLast {
        if (!project.hasProperty('packageVersion')) {
            SimpleDateFormat f = new SimpleDateFormat('yyyyMMdd')
            project.avnavVersion = f.format(new Date())
        } else {
            project.avnavVersion = packageVersion
        }
        if (!versionFile.getParentFile().isDirectory()) versionFile.getParentFile().mkdirs()
        versionFile.withWriter { wr ->
            wr.println("export default   \"$project.avnavVersion\";")
        }
    }
}

task release(type: MyNpm){
    subDir="release"
	args=["run","production"]
    setExecOverrides({
        it.environment.put('AVNAV_VERSION_FILE',versionFile.getCanonicalPath())
    })
	dependsOn npmInstall,pkgVersion
}
task debug(type: MyNpm){
    subDir="debug"
	args=["run","debug"]
    setExecOverrides({
        it.environment.put('AVNAV_VERSION_FILE',versionFile.getCanonicalPath())
    })
	dependsOn npmInstall,pkgVersion
}
