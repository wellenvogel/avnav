import java.text.SimpleDateFormat
import com.github.gradle.node.npm.task.*
buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.github.node-gradle:gradle-node-plugin:5.0.0"
    }
}

apply plugin: 'com.github.node-gradle.node'
apply plugin: 'java'

node{
    version='20.19.4'
    npmVersion='10.8.2'
    download=true
}


abstract class MyNpm extends NpmTask{
    @Internal
    def subDir
    @InputFiles
    def getInputFiles(){
        logger.debug "getInputFiles called for $name"
        return project.fileTree(project.projectDir,{
            exclude "build/**"
            exclude ".gradle/**"
        })
    }
    @OutputFiles
    def getOutputFiles(){
        logger.debug "getOutputFiles called for $name"
        return project.fileTree(project.projectDir,{
            include "build/$subDir/**"
        })
    }
    @OutputDirectory
    def getOutDir(){
        return project.file("build/$subDir")
    }
    public MyNpm(){
        outputs.upToDateWhen{
            def od=getOutDir()
            logger.debug "checking $od"
            if (! od.exists()) return false;
            return true
        }
    }
}
def computePackageVersion(){
    def rt=null
    if (!project.hasProperty('packageVersion')) {
        SimpleDateFormat f = new SimpleDateFormat('yyyyMMdd')
        rt = f.format(new Date())
    } else {
        rt= packageVersion
    }
    //logger.lifecycle("AVNAV_VERSION=${rt}")
    return rt
}

task touchLock {
    doLast{
       File f=file('package-lock.json')
       if (!f.exists()){
           f.withWriter { it << ''}
       } 
    }
}
npmInstall.dependsOn(touchLock)

task release(type: MyNpm){
    subDir="release"
	args=["run","production"]
	dependsOn npmInstall
    environment=['AVNAV_VERSION':computePackageVersion()]
}
task debug(type: MyNpm){
    subDir="debug"
	args=["run","debug"]
    environment=['AVNAV_VERSION':computePackageVersion()]
	dependsOn npmInstall
}
