
import java.text.SimpleDateFormat;
import org.redline_rpm.header.*;
def taskgroup="avnav"

buildscript{
	repositories{
		mavenCentral()
	}
    dependencies{
        classpath 'com.netflix.nebula:gradle-ospackage-plugin:12.1.0'
    }
}

repositories{
	mavenCentral()
}

description="Raspberry specfic stuff for avnav (old)"
apply plugin: 'java'

apply plugin: 'com.netflix.nebula.ospackage-base'


def BASE='raspberry'
def LEGACY="$BASE/legacy"
def versionFileName="raspi.version"
task build_raspi(type: Deb) {
    group taskgroup
    doFirst{
        assert project.hasProperty('avnavVersion'),"please provide the package version via -PavnavVersion=xxx"
        version=project.avnavVersion
        def vFile=file("$buildDir/$versionFileName")
        logger.info("vfile=${vFile}")
	    if (! vFile.getParentFile().exists()) vFile.getParentFile().mkdirs()
        vFile.withWriter {wr->
            wr.println "AVNAV_VERSION=\"$version\""
        }
    }
    //release='3'
    os = LINUX // only applied to RPM
    packageGroup='misc'
    requires 'avnav'
    requires 'parted'
    requires 'bc'
    requires 'hostapd'
    requires 'dnsmasq'
    requires 'mpg123'
    requires('ntp').or('ntpsec')
    requires 'avahi-autoipd'
    requires 'python3-smbus' //could basically also directly go to avnav - but outside rpi there is no use case
    requires('canboat','4.12',Flags.GREATER|Flags.EQUAL).or('base-files','11',Flags.LESS)
    requires 'jq'
    packageName='avnav-raspi'
    ['common','wlan0','wlan1','wlan-ap','wlan-ap2'].each { String ext->
        configurationFile("/etc/hostapd/hostapd.conf.$ext")
    }
    configurationFile("/etc/udev/rules.d/010-avnav-net.rules")
    user='root'
    into ('/usr/lib/avnav/raspberry') {
        from(BASE){
            fileMode 0755
            include 'sound.sh'
            include 'setup-helper.sh'
            include 'prestart.sh'
            include 'uart_control'
        }
        from(LEGACY){
            fileMode 0755
            include 'settime.sh'
            include 'hostapd'
            include 'restore-iptables.sh'
            include 'iptables-ext.sh'
            include 'wpa_supplicant.conf'
            include 'startup-check.sh'
            include 'wpa_action.sh'
            include 'rename-interface.sh'
            include 'checkLink.sh'
            include 'setup-*.sh'
        }
        from (LEGACY){
            include 'hosts'
            include 'avnav_server.xml'
            include 'ntp.conf'
            fileMode 0644
        }
        from(BASE) {
            include 'avnav.conf'
            fileMode 0644
        }
        from('linux'){
            include 'patchServerConfig.py'
            fileMode 0755
        }
    }
    into('/usr/lib/avnav/raspberry/xui'){
        from('raspberry/xui')
    }
    into ('/usr/lib/avnav/raspberry') {
        from(project.buildDir){
            include versionFileName
        }
    }
    into('/usr/lib/avnav/plugins/resetAlarm'){
        from('raspberry/resetAlarm')
    }
    into('/usr/lib/systemd/system/avnav.service.d'){
        from(BASE){
            include "raspberry.conf"
        }
    }
    into('/usr/lib/systemd/system'){
        from(BASE){
            include('avnav-startup.service')
        }
        from(LEGACY){
            include('avnav-iptables.service')
        }
    }
    into('/etc/network/interfaces.d'){
        from(LEGACY){
            include('lo')
            include('eth0')
            include('usb0')
            include('wlan0')
            include('wlan1')
            include('wlan-av1')
            include('wlan-ap')
            include('wlan-ap2')
        }
    }
    into('/etc/hostapd'){
        from(LEGACY){
            include('hostapd.conf*')
        }
    }
    into('/etc/sysctl.d'){
        from(BASE){
            include('99-zzavnavsysctl.conf')
        }
    }
    into('/etc'){
        from(LEGACY){
            include('iptables.ipv4.nat')
        }
    }
    into('/etc/udev/rules.d'){
        from(LEGACY){
            include('010-avnav-net.rules')
        }
    }
    into('/etc/dnsmasq.d'){
        from(LEGACY){
            include('avnav-dnsmasq.conf')
        }
    }
    into('/etc'){
        from(LEGACY){
            include('dhcpcd.enter-hook')
        }
    }
    into('/etc/modprobe.d'){
        from(LEGACY){
            include('8192cu.conf')
        }
    }
    into('/etc/avahi/services'){
        from(BASE){
            include('avnav-ssh.service')
        }
    }
    
    postInstall file("$LEGACY/postinstall")
    preInstall file('raspberry/preinstall')

}

